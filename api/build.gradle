apply from: '../appmodule.gradle'

dependencies {
    implementation project(':auth')

    implementation "androidx.compose.ui:ui:1.1.0-rc03"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1'
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'
}

Properties getLocalProperties() {
    Properties props = new Properties()
    if (file('local.properties').exists()) {
        props.load(new FileInputStream(file('local.properties')))
    }
    return props
}

def getEnvVarOrLocalProperty(String propertyName) {
    String envVar = System.getenv(propertyName)
    if (envVar == null || envVar.isEmpty()) {
        return getLocalProperties().getProperty(propertyName)
    }
    return envVar
}

android {
    buildTypes {
        debug {
            resValue("string", "reddit_api_client_id", getEnvVarOrLocalProperty("REDDIT_API_CLIENT_ID"))
        }
        release {
            resValue("string", "reddit_api_client_id", getEnvVarOrLocalProperty("REDDIT_API_CLIENT_ID"))
        }
    }
}